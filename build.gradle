plugins {
    id 'java'
}

group = 'be.raft'
version = project.plugin_version

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    //Spigot
    compileOnly "org.spigotmc:spigot-api:${project.minecraft_version}-R0.1-SNAPSHOT"

    //Other dependencies
    implementation "com.google.code.gson:gson:${project.gson_version}"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version, api_version: api_version, plugin_name: plugin_name,
                 plugin_description: plugin_description, plugin_website: plugin_website]
    inputs.property("java_version", targetJavaVersion)
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('build_plugin.yml') {
        expand "java_version": targetJavaVersion
    }
}

jar {
    destinationDirectory.set(file("/run/plugins"))
}
